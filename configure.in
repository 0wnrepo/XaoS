# Autoconf requirements
AC_PREREQ([2.61])

# information on the package
AC_INIT([XaoS], 
	[3.4], 
	[http://sourceforge.net/tracker/?group_id=5771&atid=105771])

DRIVERLIBS="lib/libui.a lib/libui-hlp.a lib/libengine.a lib/libutil.a lib/libfilter.a"

AC_CONFIG_SRCDIR(src/ui/ui.c)

AC_CONFIG_MACRO_DIR([m4])
#AC_CONFIG_AUX_DIR(build-aux)
AC_CONFIG_HEADERS([src/include/aconfig.h src/include/version.h])

# package options
AC_ARG_ENABLE([long-double],
    [AS_HELP_STRING([--enable-long-double],
    [enables use of long double for extended zoom @<:@default=check@:>@])],
    [],
    [enable_long_double=check])

AC_ARG_ENABLE([custom-formulas],
    [AS_HELP_STRING([--enable-custom-formulas],
    [enables custom formulas (requires nasm or GSL) @<:@default=check@:>@])],
    [],
    [enable_custom_formulas=check])

AC_ARG_WITH([nasm],
    [AS_HELP_STRING([--with-nasm],
    [enables custom formulas via nasm (i386 only) @<:@default=check@:>@])],
    [],
    [with_nasm=check])

AC_ARG_WITH([gsl],
    [AS_HELP_STRING([--with-gsl],
    [enables custom formulas via GSL library @<:@default=check@:>@])],
    [],
    [with_gsl=check])

AC_ARG_WITH([pthread],
    [AS_HELP_STRING([--with-pthread],
    [enables SMP support @<:@default=check@:>@])],
    [],
    [with_pthread=check])

AC_ARG_WITH([png],
    [AS_HELP_STRING([--with-png],
    [enables PNG image saving @<:@default=check@:>@])],
    [],
    [with_png=check])

AC_ARG_WITH([win32],
    [AS_HELP_STRING([--with-win32],
    [enables Win32 user interface @<:@default=check@:>@])],
    [],
    [with_win32=check])

AC_ARG_WITH([gtk],
    [AS_HELP_STRING([--with-gtk],
    [enables GTK+ user interface @<:@default=check@:>@])],
    [],
    [with_gtk=check])

AC_ARG_WITH([xshm],
    [AS_HELP_STRING([--with-xshm],
    [enables X shared memory support @<:@default=check@:>@])],
    [],
    [with_xshm=check])

# checks for system type
AC_CANONICAL_HOST
AC_USE_SYSTEM_EXTENSIONS

# checks for programs
AC_PROG_CC_STDC
AC_PROG_CPP
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_CHECK_PROGS([AR], [ar])

# checks for libraries
AC_CHECK_LIB([m], [pow])

# checks for header files
AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([OS.h alloca.h ddraw.h errno.h fcntl.h float.h libintl.h limits.h locale.h malloc.h stddef.h stdlib.h string.h sys/time.h sys/timeb.h unistd.h])

# checks for types
AC_TYPE_SIGNAL
AC_TYPE_PID_T
AC_TYPE_SIZE_T

if test "x$enable_long_double" == xcheck; then
    # long double is known to be slow on architectures other than x86
    case $host in
        i?86-*-*) AC_TYPE_LONG_DOUBLE;;
    esac
elif test "x$enable_long_double" == xyes; then
    AC_TYPE_LONG_DOUBLE
    if test "x$ac_cv_type_long_double" != xyes; then
        AC_MSG_FAILURE(
	    [--enable-long-double was given, but test for long double failed.])
    fi
fi

# checks for structures
AC_STRUCT_TM

# checks for compiler characteristics
AC_C_INLINE
AC_C_CONST
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AX_CC_MAXOPT

# checks for library functions
AC_FUNC_ALLOCA
AC_CHECK_FUNCS([ftime getcwd gettimeofday memchr memmove memset pow select setlocale sqrt strdup strerror strstr sleep setitimer ftime finite fabsl _fabsl __fabsl])

# checks for packages
AS_IF([test "x$with_gtk" != xno],
  [PKG_CHECK_MODULES([GTK], [gtk+-2.0 >= 2.2 glib-2.0 >= 2.2], 
    [AC_DEFINE([GTK_DRIVER], [1], [Define if building GTK driver])
     DRIVERDIRS="$DRIVERDIRS ui/ui-drv/gtk"
     DRIVERLIBS="$DRIVERLIBS lib/libuigtk.a"
     CFLAGS="$CFLAGS $GTK_CFLAGS"
     LIBS="$LIBS $GTK_LIBS"
    ],
    [if test "x$with_gtk" == xyes; then
       AC_MSG_FAILURE(
	 [--with-gtk was given, but test for GTK failed])
     fi
    ])])

# checks for system services
AC_PATH_XTRA
AS_IF([test "x$no_x" != xyes],
  [AC_DEFINE([X11_DRIVER], [1], [Define if building X11 driver])
   DRIVERDIRS="$DRIVERDIRS ui/ui-drv/x11"
   DRIVERLIBS="$DRIVERLIBS lib/libuix11.a"
   CFLAGS="$CFLAGS $X_CFLAGS"
   LIBS="$LIBS $X_PRE_LIBS $X_LIBS $X_EXTRA_LIBS -lX11"
  ],
  [if test "x$with_x" == xyes; then
     AC_MSG_FAILURE(
       [--with-x was given, but test for X failed])
   fi
  ])

#AM_GNU_GETTEXT([external])

# ugly stuff required by current makefiles
# (should be removed after move to automake)
REALTOPDIR=`nolinks=1; pwd`
AC_SUBST(REALTOPDIR)
BINPATH="$REALTOPDIR/bin"
AC_SUBST(BINPATH)
SRCPATH="$REALTOPDIR/src"
AC_SUBST(SRCPATH)
LIBPATH="$SRCPATH/lib"
AC_SUBST(LIBPATH)
INCDIR="-I${SRCPATH}/include"
AC_SUBST(INCDIR)
CFLAGS="$CFLAGS $INCDIR"
AC_SUBST(POSTCOMPILE)

AC_SUBST(ASM_CMPLX_O)
AC_SUBST(USE_NLS)

AC_DEFINE_UNQUOTED([DATAPATH], ["$datadir"], [Location of data files])
AC_DEFINE([NOASSEMBLY], [1], [Define to prevent custom asm mandelbrot])

AC_SUBST([DRIVERDIRS])
AC_SUBST([DRIVERLIBS])

AC_CONFIG_FILES([Makefile 
		 doc/Makefile 
		 help/Makefile 
		 src/Makefile 
		 src/engine/Makefile 
		 src/filter/Makefile 
		 src/sffe/Makefile
		 src/ui/Makefile 
		 src/ui/ui-drv/x11/Makefile 
		 src/ui/ui-drv/win32/Makefile 
		 src/ui/ui-drv/gtk/Makefile 
		 src/ui-hlp/Makefile 
		 src/util/Makefile])
AC_OUTPUT

